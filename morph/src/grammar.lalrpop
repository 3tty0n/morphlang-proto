use std::str::{FromStr};

use crate::ast::{Stmt, Expr, Function, BinOp, Program, Module};

grammar;

pub Program: Box<Program> = {
    <ms:Modules> <fs:Functions> => Box::new(Program::Program(ms, fs))
};

pub Module: Box<Module> = {
    "open" <id:Ident> => Box::new(Module::Module(id))
};

Modules: Vec<Box<Module>> = {
    Module*
}

pub Stmt: Box<Stmt> = {
    "for" <id:Ident> "=" <start:Expr> "to" <end:Expr> "do" <body:Expr> "done"
        => Box::new(Stmt::For(id, start, end, body))
}

pub Expr: Box<Expr> = {
    #[precedence(level="0")]
    "()" => Box::new(Expr::Unit),
    Num => Box::new(Expr::Number(<>)),
    FNum => Box::new(Expr::FNumber(<>)),
    Ident => Box::new(Expr::Variable(<>)),
    "(" <Expr> ")",
    #[precedence(level="1")]
    #[assoc(side="left")]
    <l:Expr> "+" <r:Expr> => Box::new(Expr::BinOp(l, BinOp::Plus, r)),
    <l:Expr> "-" <r:Expr> => Box::new(Expr::BinOp(l, BinOp::Minus, r)),
    <l:Expr> "/" <r:Expr> => Box::new(Expr::BinOp(l, BinOp::Slash, r)),
    <l:Expr> "*" <r:Expr> => Box::new(Expr::BinOp(l, BinOp::Star, r)),
    <l:Expr> "<" <r:Expr> => Box::new(Expr::BinOp(l, BinOp::LT, r)),
    <l:Expr> ">" <r:Expr> => Box::new(Expr::BinOp(l, BinOp::GT, r)),
    <l:Expr> "<=" <r:Expr> => Box::new(Expr::BinOp(l, BinOp::LE, r)),
    <l:Expr> ">=" <r:Expr> => Box::new(Expr::BinOp(l, BinOp::GE, r)),
    "if" <cond:Expr> "then" <e1:Expr> "else" <e2:Expr> => Box::new(Expr::IfElse(cond, e1, e2)),
    <id:Ident> "(" <args:Arguments> ")" => Box::new(Expr::FunApp(id, args)),
    <modid:Ident> "." <funid:Ident> "("<args:Arguments> ")" => Box::new(Expr::ModFunApp(modid, funid, args)),
    #[precedence(level="2")]
    #[assoc(side="right")]
    "let" <id:Ident> "=" <body:Expr> "in" <e:Expr> => Box::new(Expr::Let(id, body, e)),
    <stmt:Stmt> ";" <e:Expr> => Box::new(Expr::LetStmt("_".to_string(), stmt, e))
};

pub Function: Box<Function> = {
    "function" <id:Ident> "(" <args:Arguments> ")" "=" <expr:Expr> ";;"
        => Box::new(Function::Fun(id, args, expr)),
    "router" <id:Ident> "(" <args:Arguments> ")" "=" <expr:Expr> ";;"
        => Box::new(Function::Router(id, args, expr)),
    "coordinator" <id:Ident> "(" <args:Arguments> ")" "=" <expr:Expr> ";;"
        => Box::new(Function::Coordinator(id, args, expr)),
};

Functions: Vec<Box<Function>> = {
    Function*
};

Arguments = Comma<Expr>;

Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};

Ident: String = {
    r"[a-zA-Z][a-zA-Z0-9_]*" => String::from(<>)
};

Num: i32 = <s:r"[0-9]+"> => i32::from_str(s).unwrap();

FNum: f64 = <s:r"[0-9]*[.][0-9]+"> => f64::from_str(s).unwrap();
